// start
expression  : symbol
            | ANCHOR
            | EPSILON
            | DOTANY
            | pos_chars
            | neg_chars
            | kleene
            | option
            | concat
            | disjunction

// regex rules
kleene      :     expression  "*"
option      : "(" expression ")?"
concat      : "(" expression  " "  expression ")"
disjunction : "(" expression " + " expression ")"

// character classes
pos_chars   : "["  (char_range | symbol)+ "]"
neg_chars   : "[^" (char_range | symbol)+ "]"
char_range  : symbol "-" symbol

// atoms
symbol      : ESCAPED
            | ["\\"] /./
ESCAPED     : /\\[rnt]/
EPSILON     : "@epsilon"
DOTANY      : "@any"
ANCHOR      : "<ASTART>" | "<AEND>"